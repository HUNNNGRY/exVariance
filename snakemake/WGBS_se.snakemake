include: 'common.snakemake'

root_dir=config.get('root_dir')
output_dir=config.get('output_dir')


## 以下工作在python2.7环境下运行

rule all:
    input:
        expand('{output_dir}/methylation_levels/{sample_id}.wig.gz',output_dir=config.get('output_dir'),sample_id=sample_ids),
        expand('{output_dir}/methylation_levels/{sample_id}.ATCGmap.gz',output_dir=config.get('output_dir'),sample_id=sample_ids),
        expand('{output_dir}/methylation_levels/{sample_id}.CGmap.gz',output_dir=config.get('output_dir'),sample_id=sample_ids)

## This step is not suggested for RRBS library, as reads from RRBS library would more likely from the same location
rule filter_reads:
    input:
        read=config.get('data_dir') + '/fasta/{sample_id}.fasta.gz'
    output:
        read='{output_dir}/filter_reads/{sample_id}.filtered.fastq.gz'
    conda:
        "envs/WGBS.yaml"
    log:
        '{output_dir}/log/WGBS/filter_reads/{sample_id}.log'
    shell:
        '''FilterReads.py \
            -i {input.read} \
            -o {output.read} \
            > {log} 2>&1
        '''

rule build_index:
    input:
        fasta=config.get("genome_dir") + '/fasta/genome.fa'
    output:
        directory('{output_dir}/genome_index/genome.fa_bowtie2')
    params:
        '{output_dir}/genome_index'
    conda:
        "envs/WGBS.yaml"
    log:
        '{output_dir}/log/WGBS/build_index.log'
    shell:
        '''bs_seeker2-build.py \
            -f {input.fasta} \
            -d {params} \
            --aligner=bowtie2 \
            > {log} 2>&1
        '''

rule mapping:
    input:
        read='{output_dir}/filter_reads/{sample_id}.filtered.fastq.gz',
        database=directory('{output_dir}/genome_index/genome.fa_bowtie2')
    output:
        bam='{output_dir}/bam/{sample_id}.bam'
    params:
        fasta=config.get("genome_dir") + '/fasta/genome.fa',
        genome_dir='{output_dir}/genome_index'
    conda:
        "envs/WGBS.yaml"
    log:
        '{output_dir}/log/WGBS/mapping/{sample_id}.log'
    shell:
        '''bs_seeker2-align.py \
            -i {input.read} \
            -m 3 \
            --aligner=bowtie2 \
            -d {params.genome_dir} \
            -o {output.bam} \
            -f bam \
            -g {params.fasta} \
            > {log} 2>&1
        '''

rule call_the_methylation_levels:
    input:
        bam='{output_dir}/bam/{sample_id}.bam',
        database = directory('{output_dir}/genome_index/genome.fa_bowtie2')
    output:
        '{output_dir}/methylation_levels/{sample_id}.wig.gz',
        '{output_dir}/methylation_levels/{sample_id}.ATCGmap.gz',
        '{output_dir}/methylation_levels/{sample_id}.CGmap.gz'
    params:
        output = '{output_dir}/methylation_levels/{sample_id}'
    conda:
        "envs/WGBS.yaml"
    log:
        '{output_dir}/log/WGBS/call_the_methylation_levels/{sample_id}.log'
    shell:
        '''bs_seeker2-call_methylation.py \
            -x \
            -i {input.bam} \
            --db {input.database} \
            -o {params.output} \
            > {log} 2>&1
        '''