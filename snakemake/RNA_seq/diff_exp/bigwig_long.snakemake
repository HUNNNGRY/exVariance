# vim: syntax=python tabstop=4 expandtab
# coding: utf-8

#---------------------------
# @author: Shang Zhang
# @email: shang_zhang@foxmail.com
# @date: Sep, 3rd, 2020
#---------------------------


###------------------------The output section---------------------------###

rule sort_bam_by_coord:
    input:
        '{output_dir}/bam/{sample_id}/{map_step}.bam'
    output:
        '{output_dir}/bam_sorted_by_coord/{sample_id}/{map_step}.bam'
    params:
        temp_dir=config.get('temp_dir')
    conda:
        "../../envs/bigwig.yaml"
    log:
        '{output_dir}/log/bigwig/sort_bam_by_coord/{sample_id}_{map_step}.log'
    shell:
        '''samtools sort -T {params.temp_dir} -o {output} {input} > {log} 2>&1
        '''

rule index_bam_by_coord:
    input:
        '{output_dir}/bam_sorted_by_coord/{sample_id}/{map_step}.bam'
    output:
        '{output_dir}/bam_sorted_by_coord/{sample_id}/{map_step}.bam.bai'
    conda:
        "../../envs/bigwig.yaml"
    log:
        '{output_dir}/log/bigwig/index_bam_by_coord/{sample_id}_{map_step}.log'
    shell:
        '''samtools index {input} > {log} 2>&1
        '''

rule genomecov:
    input:
        '{output_dir}/bam_sorted_by_coord/{sample_id}/{map_step}.bam'
    output:
        '{output_dir}/bedgraph/{sample_id}.{map_step}.{strand}.bedGraph'
    params:
        #pc='-pc' if config['paired_end'] else '',
        pc='',
        temp_dir=config.get('temp_dir')
    wildcard_constraints:
        strand='[+-]'
    conda:
        "../../envs/bigwig.yaml"
    shell:
        '''bedtools genomecov -strand {wildcards.strand} -bg {params.pc} -split -ibam {input} \
            | LC_COLLATE=C sort -T {params.temp_dir} -k1,1 -k2,2n > {output}
        '''

genome_dir=config.get('genome_dir')
rule bedgraph_to_bigwig:
    input:
        bedgraph='{output_dir}/bedgraph/{sample_id}.{map_step}.{strand}.bedGraph',
        chrom_sizes=lambda wildcards: genome_dir + '/chrom_sizes/' + re.sub('_rmdup$', '', wildcards.map_step)
    output:
        '{output_dir}/bigwig/{sample_id}.{map_step}.{strand}.bigWig'
    conda:
        "../../envs/bigwig.yaml"
    log:
        '{output_dir}/log/bigwig/bedgraph_to_bigwig/{sample_id}_{map_step}_{strand}.log'
    shell:
        '''bedGraphToBigWig {input.bedgraph} {input.chrom_sizes} {output} > {log} 2>&1
        '''

rule normalize_bigwig:
    input:
        bam='{output_dir}/bam/{sample_id}/{map_step}.bam',
        bigwig='{output_dir}/bigwig/{sample_id}.{map_step}.{strand}.bigWig',
        chrom_sizes=lambda wildcards: genome_dir + '/chrom_sizes/' + re.sub('_rmdup$', '', wildcards.map_step)
    output:
        bigwig='{output_dir}/bigwig_normalized/{sample_id}.{map_step}.{strand}.bigWig',
        bedgraph=temp('{output_dir}/bigwig_normalized/{sample_id}.{map_step}.{strand}.bedGraph')
    wildcard_constraints:
        strand='[+-]'
    conda:
        "../../envs/bigwig.yaml"
    shell:
        r'''read_depth=`bamtools count -in {input.bam}`
        bigWigToBedGraph {input.bigwig} stdout \
            | awk -v d="$read_depth" 'BEGIN{{OFS="\t";FS="\t"}}{{print $1,$2,$3,1000000.0*2*$4/d}}' > {output.bedgraph}
        bedGraphToBigWig {output.bedgraph} {input.chrom_sizes} {output.bigwig}
        '''

rule map_bigwig_to_pseudo_genome:
    input:
        bigwig='{output_dir}/bigwig_normalized/{sample_id}.{map_step}.{strand}.bigWig',
        annotation=lambda wildcards: expand(genome_dir + '/bed/pseudo_genome.{gene_type}.bed', 
            gene_type=re.sub(r'_rmdup$', '', wildcards.map_step)),
        chrom_sizes=lambda wildcards: expand(genome_dir + '/chrom_sizes/pseudo_genome.{gene_type}',
            gene_type=re.sub(r'_rmdup$', '', wildcards.map_step))
    output:
        bigwig='{output_dir}/bigwig_pseudo_genome/{sample_id}.{map_step}.{strand}.bigWig',
        bedgraph=temp('{output_dir}/bigwig_pseudo_genome/{sample_id}.{map_step}.{strand}.bedGraph')
    conda:
        "../../envs/bigwig.yaml"
    wildcard_constraints:
        map_step='(circRNA)|(circRNA_rmdup)'
    shell:
        r'''bigWigToBedGraph {input.bigwig} stdout \
            | awk 'BEGIN{{OFS="\t";FS="\t"}} FNR==NR{{chrom[$4]=$1;start[$4]=$2;next}} {{if($1 in chrom) print chrom[$1],start[$1]+$2,start[$1]+$3,$4}}' \
                {input.annotation} - > {output.bedgraph}
            bedGraphToBigWig {output.bedgraph} {input.chrom_sizes} {output.bigwig}
        '''

rule map_bam_to_pseudo_genome_1_2:
    input:
        bam='{output_dir}/bam/{sample_id}/{map_step}.bam',
        bed=lambda wildcards: expand(genome_dir + '/bed/pseudo_genome.{gene_type}.bed', 
            gene_type=re.sub(r'_rmdup$', '', wildcards.map_step)),
        chrom_sizes=lambda wildcards: expand(genome_dir + '/chrom_sizes/pseudo_genome.{gene_type}',
            gene_type=re.sub(r'_rmdup$', '', wildcards.map_step))
    output:
        unsorted_bam=temp('{output_dir}/bam_pseudo_genome/{sample_id}/{map_step}.unsorted.bam')
    wildcard_constraints:
        map_step='(circRNA)|(circRNA_rmdup)'
    conda:
        "../../envs/bigwig.yaml"
    log:
        '{output_dir}/log/bigwig/map_bam_to_pseudo_genome_1_2/{sample_id}_{map_step}.log'
    shell:
        '''{root_dir}/bin/preprocess.py map_bam_to_pseudo_genome -i {input.bam} \
            --bed {input.bed} --chrom-sizes {input.chrom_sizes} \
            -o {output.unsorted_bam} > {log} 2>&1
        '''

rule map_bam_to_pseudo_genome_2_2:
    input:
        unsorted_bam='{output_dir}/bam_pseudo_genome/{sample_id}/{map_step}.unsorted.bam',
    output:
        bam='{output_dir}/bam_pseudo_genome/{sample_id}/{map_step}.bam',
        bai='{output_dir}/bam_pseudo_genome/{sample_id}/{map_step}.bam.bai'
    wildcard_constraints:
        map_step='(circRNA)|(circRNA_rmdup)'
    conda:
        "../../envs/bigwig.yaml"
    log:
        '{output_dir}/log/bigwig/map_bam_to_pseudo_genome_2_2/{sample_id}_{map_step}.log'
    shell:
        '''
        samtools sort {input.unsorted_bam} > {output.bam}
        samtools index {output.bam} > {log} 2>&1
        '''