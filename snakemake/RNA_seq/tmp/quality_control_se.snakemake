include: 'common.snakemake'

import os

def get_all_inputs(wildcards):
    available_inputs = dict(
        fastqc_html=expand('{output_dir}/fastqc/{sample_id}_fastqc.html',
            output_dir=output_dir, sample_id=sample_ids),
        multi_html=expand('{output_dir}/summary/fastqc.html',
            output_dir=output_dir)
    )
    enabled_inputs = list(available_inputs.keys())
    inputs = []
    for key, l in available_inputs.items():
        if key in enabled_inputs:
            inputs += l
    return inputs

rule all:
    input:
        get_all_inputs

rule fastqc:
    input:
        auto_gzip_input(data_dir + '/fastq/{sample_id}.fastq')
    output:
        html='{output_dir}/fastqc/{sample_id}_fastqc.html',
        zip='{output_dir}/fastqc/{sample_id}_fastqc.zip'
    params:
        output_prefix='{output_dir}/fastqc/',
        temp_dir=config['temp_dir']
    conda:
        "envs/quality_control.yaml"
    log:
        '{output_dir}/log/fastqc/{sample_id}'
    shell:
        '''fastqc -q -o {params.output_prefix} -d {params.temp_dir} {input} > {log} 2>&1
        '''

rule multiqc:
    input:
        fastqc=expand('{output_dir}/fastqc/{sample_id}_fastqc.zip',
            output_dir=output_dir, sample_id=sample_ids)
    output:
        html='{output_dir}/summary/fastqc.html',
        data=directory('{output_dir}/summary/fastqc_data')
    params:
        fastqc_dir='{output_dir}/fastqc'
    conda:
        "envs/quality_control.yaml"
    shell:
        '''multiqc -m fastqc -n {output.html} {params.fastqc_dir}
        '''