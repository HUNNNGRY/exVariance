include: 'common.snakemake'

output_dir = config.get("output_dir")

def get_all_inputs(wildcards):
    available_inputs = dict(
        fastqc_html=expand('{output_dir}/fastqc/{sample_id}_{mate_index}_fastqc.html', 
            output_dir=output_dir, sample_id=sample_ids, mate_index=[1, 2]),
        multiqc_html=expand('{output_dir}/summary/fastqc.html', output_dir=output_dir)
    )
    enabled_inputs = list(available_inputs.keys())
    inputs = []
    for key, l in available_inputs.items():
        if key in enabled_inputs:
            inputs += l
    return inputs

rule all:
    input:
        get_all_inputs
    
rule fastqc_pe:
    input:
        auto_gzip_input(config.get('data_dir') + '/fastq/{sample_id}_{mate_index}.fastq')
    output:
        html='{output_dir}/fastqc/{sample_id}_{mate_index}_fastqc.html',
        zip='{output_dir}/fastqc/{sample_id}_{mate_index}_fastqc.zip'
    params:
        output_prefix='{output_dir}/fastqc/',
        temp_dir=config['temp_dir']
    wildcard_constraints:
        mate_index='[12]'
    conda:
        "envs/quality_control.yaml"
    log:
        '{output_dir}/log/quality_control/fastqc/{sample_id}_{mate_index}.log'
    shell:
        '''rm -rf {params.temp_dir};
            mkdir {params.temp_dir};
            fastqc -q -o {params.output_prefix} -d {params.temp_dir} {input} > {log} 2>&1
        '''

rule multiqc_pe:
    input:
        fastqc=expand('{output_dir}/fastqc/{sample_id}_{mate_index}_fastqc.zip',
            output_dir=output_dir, sample_id=sample_ids, mate_index=[1, 2])
    output:
        html='{output_dir}/summary/fastqc.html',
        data=directory('{output_dir}/summary/fastqc_data')
    params:
        fastqc_dir='{output_dir}/fastqc'
    wildcard_constraints:
        mate_index='[12]'
    conda:
        "envs/quality_control.yaml"
    log:
        '{output_dir}/log/quality_control/multiqc.log'
    shell:
        '''multiqc -m fastqc -n {output.html} {params.fastqc_dir} > {log} 2>&1
        '''