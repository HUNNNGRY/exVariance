include: 'common.snakemake'

root_dir=config.get('root_dir')
output_dir=config.get('output_dir')


## 以下工作在python2.7环境下运行

rule all:
    input:
        expand('{output_dir}/methylation_levels/{sample_id}.wig.gz',output_dir=config.get('output_dir'),sample_id=sample_ids),
        expand('{output_dir}/methylation_levels/{sample_id}.ATCGmap.gz',output_dir=config.get('output_dir'),sample_id=sample_ids),
        expand('{output_dir}/methylation_levels/{sample_id}.CGmap.gz',output_dir=config.get('output_dir'),sample_id=sample_ids)

rule build_index:
    input:
        fasta=config.get("genome_dir") + '/fasta/genome.fa'
    output:
        directory('{output_dir}/genome_index/genome.fa_rrbs_40_400_bowtie2')
    params:
        '{output_dir}/genome_index'
    conda:
        "envs/RRBS.yaml"
    log:
        '{output_dir}/log/RRBS/build_index.log'
    shell:
        '''bs_seeker2-build.py \
            -f {input.fasta} \
            -d {params} \
            -r \
            -l 40 \
            -u 400 \
            --aligner=bowtie2 \
            > {log} 2>&1
        '''

rule mapping:
    input:
        read=config.get('data_dir') + '/fasta/{sample_id}.fasta.gz',
        database=directory('{output_dir}/genome_index/genome.fa_rrbs_40_400_bowtie2')
    output:
        bam='{output_dir}/bam/{sample_id}.bam'
    params:
        fasta=config.get("genome_dir") + '/fasta/genome.fa',
        database = '{output_dir}/genome_index'
    conda:
        "envs/RRBS.yaml"
    log:
        '{output_dir}/log/RRBS/mapping/{sample_id}.log'
    shell:
        '''bs_seeker2-align.py \
            -i {input.read} \
            --aligner=bowtie2 \
            --bt2--end-to-end \
            -d {params.database} \
            -o {output.bam} \
            -f bam \
            -g {params.fasta} \
            -r \
            --low=40 \
            --up=400 \
            > {log} 2>&1
        '''

rule call_the_methylation_levels:
    input:
        bam='{output_dir}/bam/{sample_id}.bam',
        database = directory('{output_dir}/genome_index/genome.fa_rrbs_40_400_bowtie2')
    output:
        '{output_dir}/methylation_levels/{sample_id}.wig.gz',
        '{output_dir}/methylation_levels/{sample_id}.ATCGmap.gz',
        '{output_dir}/methylation_levels/{sample_id}.CGmap.gz'
    params:
        output = '{output_dir}/methylation_levels/{sample_id}'
    conda:
        "envs/RRBS.yaml"
    log:
        '{output_dir}/log/WGBS/call_the_methylation_levels/{sample_id}.log'
    shell:
        '''bs_seeker2-call_methylation.py \
            -x \
            -i {input.bam} \
            --db {input.database} \
            -o {params.output} \
            > {log} 2>&1
        '''